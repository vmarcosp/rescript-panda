exception InvalidPandaConfig(string)

module Process = NodeJs.Process

let createBindingsModule = (config: Config.t) => {
  open ReScriptAST

  let styledSystemDir = NodeJs.Path.join([
    Process.process->Process.cwd,
    config.outdir,
    Constants.mainModuleFileName,
  ])

  let nodes = [
    TypeDeclaration({
      name: "styleObject",
      type_: Record([("backgroundColor", String), ("color", String)]),
    }),
    ExternalDeclaration(
      Module(`${config.importMap}/css`),
      Identifier("css"),
      Function([UserDefinedType("styleObject")], String),
      "css",
    ),
  ]

  let bindings = nodes->ReScriptCodegen.generate

  let fileContent = `
    // âš ï¸ Generated by rescript-panda. Only edit this file if you want to stop using rescript-panda codegen.
    ${bindings}
  `

  NodeJs.Fs.writeFileSync(styledSystemDir, fileContent->NodeJs.Buffer.fromString)

  Js.log("âœ… Created PandaCSS module.")
}

let run = async () => {
  let config = await Config.get()
  switch config {
  | Ok(config) => createBindingsModule(config)
  | Error(error) =>
    switch error {
    | MissingImportMap =>
      Console.log(
        `You forgot to define an ${Chalk.green(
            "importMap",
          )} in your Panda config. \nThe importMap field is required for rescript-panda to generate bindings properly.\nðŸ”Ž Check out https://panda-css.com/docs/references/config#importmap`,
      )
    | Otherwise => Console.log("Something went wrong.")
    }
  }
}
